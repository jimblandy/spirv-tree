;; Image texel fetches with `IndexBoundsCheckPolicy::ReadZeroSkipWrite`.
;; Validates in Vulkan 1.2 environment

                        OpCapability Shader
                        OpCapability ImageQuery
             %glsl450 = OpExtInstImport "GLSL.std.450"
                        OpMemoryModel Logical GLSL450

                        OpEntryPoint Fragment %fragment "fragment"
                                              %input_coords %input_index %input_lod %input_sample
                                              %var_source %var_arr_source %var_ssource %var_arr_ssource
                                              %var_mssource
                                              %output
                        OpExecutionMode %fragment OriginUpperLeft

                        ;; Required decorations for uniform images
                        OpDecorate %var_source DescriptorSet 0
                        OpDecorate %var_source Binding 0
                        OpDecorate %var_arr_source DescriptorSet 0
                        OpDecorate %var_arr_source Binding 0
                        OpDecorate %var_ssource DescriptorSet 0
                        OpDecorate %var_ssource Binding 0
                        OpDecorate %var_arr_ssource DescriptorSet 0
                        OpDecorate %var_arr_ssource Binding 0
                        OpDecorate %var_mssource DescriptorSet 0
                        OpDecorate %var_mssource Binding 0

                        ;; Required decorations for interface variables
                        OpDecorate %input_coords Location 0
                        OpDecorate %input_index Location 1
                        OpDecorate %input_lod Location 2
                        OpDecorate %input_sample Location 3
                        OpDecorate %output Location 0

                %void = OpTypeVoid
                %bool = OpTypeBool
                 %int = OpTypeInt 32 1
                 %f32 = OpTypeFloat 32

             %v2_bool = OpTypeVector %bool 2
             %v3_bool = OpTypeVector %bool 3
              %v2_int = OpTypeVector %int 2
              %v3_int = OpTypeVector %int 3
              %v4_f32 = OpTypeVector %f32 4

          %ptr_in_int = OpTypePointer Input %int
       %ptr_in_v2_int = OpTypePointer Input %v2_int
      %ptr_out_v4_f32 = OpTypePointer Output %v4_f32

            %f32_zero = OpConstantNull %f32
         %v4_f32_zero = OpConstantNull %v4_f32

                 %one = OpConstant %int 1
              %v2_one = OpConstantComposite %v2_int %one %one
              %v3_one = OpConstantComposite %v3_int %one %one %one

                        ;; Inputs and outputs for %fragment entry point
        %input_coords = OpVariable %ptr_in_v2_int Input
         %input_index = OpVariable %ptr_in_int Input
           %input_lod = OpVariable %ptr_in_int Input
        %input_sample = OpVariable %ptr_in_int Input
              %output = OpVariable %ptr_out_v4_f32 Output %v4_f32_zero

                        ;; A storage image.
            %image_2d = OpTypeImage %f32 2D 0 0 0 2 Rgba32f
        %ptr_image_2d = OpTypePointer UniformConstant %image_2d
          %var_source = OpVariable %ptr_image_2d UniformConstant

                        ;; An arrayed storage image.
        %image_arr_2d = OpTypeImage %f32 2D 0 1 0 2 Rgba32f
    %ptr_image_arr_2d = OpTypePointer UniformConstant %image_arr_2d
      %var_arr_source = OpVariable %ptr_image_arr_2d UniformConstant

                        ;; A sampled image.
           %simage_2d = OpTypeImage %f32 2D 0 0 0 1 Rgba32f
       %ptr_simage_2d = OpTypePointer UniformConstant %simage_2d
         %var_ssource = OpVariable %ptr_simage_2d UniformConstant

                        ;; An arrayed sampled image.
       %simage_arr_2d = OpTypeImage %f32 2D 0 1 0 1 Rgba32f
   %ptr_simage_arr_2d = OpTypePointer UniformConstant %simage_arr_2d
     %var_arr_ssource = OpVariable %ptr_simage_arr_2d UniformConstant

                        ;; A multisampled image.
          %msimage_2d = OpTypeImage %f32 2D 0 0 1 1 Rgba32f
      %ptr_msimage_2d = OpTypePointer UniformConstant %msimage_2d
        %var_mssource = OpVariable %ptr_msimage_2d UniformConstant

       %fragment_type = OpTypeFunction %void

          %fetch_type = OpTypeFunction %v4_f32 %v2_int %int %int %int

               %fetch = OpFunction %v4_f32 None %fetch_type
              %coords = OpFunctionParameter %v2_int
               %index = OpFunctionParameter %int
                 %lod = OpFunctionParameter %int
              %sample = OpFunctionParameter %int

            %ob_label = OpLabel
              %source = OpLoad %image_2d %var_source
          %arr_source = OpLoad %image_arr_2d %var_arr_source
             %ssource = OpLoad %simage_2d %var_ssource
         %arr_ssource = OpLoad %simage_arr_2d %var_arr_ssource
            %mssource = OpLoad %msimage_2d %var_mssource

;; No level of detail or array index.
             %bounds1 = OpImageQuerySize %v2_int %source
       %coords_conds1 = OpULessThan %v2_bool %coords %bounds1
        %coords_cond1 = OpAll %bool %coords_conds1
                        OpSelectionMerge %merge1 None
                        OpBranchConditional %coords_cond1 %read1 %merge1

               %read1 = OpLabel
         %real_texel1 = OpImageRead %v4_f32 %source %coords
                        OpBranch %merge1

              %merge1 = OpLabel
              %texel1 = OpPhi %v4_f32 %real_texel1 %read1 %v4_f32_zero %ob_label

;; Level of detail, no array index
             %levels2 = OpImageQueryLevels %int %ssource
           %lod_cond2 = OpULessThan %bool %lod %levels2
                        OpSelectionMerge %merge2 None
                        OpBranchConditional %lod_cond2 %check_coords2 %merge2

       %check_coords2 = OpLabel
             %bounds2 = OpImageQuerySizeLod %v2_int %ssource %lod
       %coords_conds2 = OpULessThan %v2_bool %coords %bounds2
        %coords_cond2 = OpAll %bool %coords_conds2
                        OpBranchConditional %coords_cond2 %read2 %merge2

               %read2 = OpLabel
         %real_texel2 = OpImageFetch %v4_f32 %ssource %coords Lod %lod
                        OpBranch %merge2

              %merge2 = OpLabel
              %texel2 = OpPhi %v4_f32 %real_texel2 %read2 %v4_f32_zero %check_coords2 %v4_f32_zero %merge1

;; Array, no level of detail
          %coords_ix3 = OpCompositeConstruct %v3_int %coords %index
             %bounds3 = OpImageQuerySize %v3_int %arr_source
       %coords_conds3 = OpULessThan %v3_bool %coords_ix3 %bounds3
        %coords_cond3 = OpAll %bool %coords_conds3
                        OpSelectionMerge %merge3 None
                        OpBranchConditional %coords_cond3 %read3 %merge3

               %read3 = OpLabel
         %real_texel3 = OpImageRead %v4_f32 %arr_source %coords_ix3
                        OpBranch %merge3

              %merge3 = OpLabel
              %texel3 = OpPhi %v4_f32 %real_texel3 %read3 %v4_f32_zero %merge2

;; Array index and level of detail
             %levels4 = OpImageQueryLevels %int %arr_ssource
           %lod_cond4 = OpULessThan %bool %lod %levels4
                        OpSelectionMerge %merge4 None
                        OpBranchConditional %lod_cond4 %check_coords4 %merge4

       %check_coords4 = OpLabel
          %coords_ix4 = OpCompositeConstruct %v3_int %coords %index
             %bounds4 = OpImageQuerySizeLod %v3_int %arr_ssource %lod
       %coords_conds4 = OpULessThan %v3_bool %coords_ix4 %bounds4
        %coords_cond4 = OpAll %bool %coords_conds4
                        OpBranchConditional %coords_cond4 %read4 %merge4

               %read4 = OpLabel
         %real_texel4 = OpImageFetch %v4_f32 %arr_ssource %coords_ix4 Lod %lod
                        OpBranch %merge4

              %merge4 = OpLabel
              %texel4 = OpPhi %v4_f32 %real_texel4 %read4 %v4_f32_zero %check_coords4 %v4_f32_zero %merge3

;; Multisampled.
            %samples5 = OpImageQuerySamples %int %mssource
       %samples_cond5 = OpULessThan %bool %sample %samples5
                        OpSelectionMerge %merge5 None
                        OpBranchConditional %samples_cond5 %check_coords5 %merge5

       %check_coords5 = OpLabel
             %bounds5 = OpImageQuerySize %v2_int %mssource
       %coords_conds5 = OpULessThan %v2_bool %coords %bounds5
        %coords_cond5 = OpAll %bool %coords_conds5
                        OpBranchConditional %coords_cond5 %read5 %merge5

               %read5 = OpLabel
         %real_texel5 = OpImageFetch %v4_f32 %mssource %coords Sample %sample
                        OpBranch %merge5

              %merge5 = OpLabel
              %texel5 = OpPhi %v4_f32 %v4_f32_zero %merge4 %v4_f32_zero %check_coords5 %real_texel5 %read5

             %result2 = OpFAdd %v4_f32 %texel1 %texel2
             %result3 = OpFAdd %v4_f32 %result2 %texel3
             %result4 = OpFAdd %v4_f32 %result3 %texel4
             %result5 = OpFAdd %v4_f32 %result4 %texel5
                        OpReturnValue %result5
                        OpFunctionEnd

                        ;; Entry point.
            %fragment = OpFunction %void None %fragment_type

           %ob_labelf = OpLabel
             %fcoords = OpLoad %v2_int %input_coords
              %findex = OpLoad %int %input_index
                %flod = OpLoad %int %input_lod
             %fsample = OpLoad %int %input_sample
             %fresult = OpFunctionCall %v4_f32 %fetch %fcoords %findex %flod %fsample
                        OpStore %output %fresult
                        OpReturn
                        OpFunctionEnd
